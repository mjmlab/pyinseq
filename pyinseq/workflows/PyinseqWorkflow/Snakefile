#!/usr/bin/python

"""

Snakefile for pyinseq pipeline

"""
import time
from pathlib import Path
# Setup logger before importing modules
from pyinseq.logger import pyinseq_logger, add_fileHandler, add_streamHandler
pyinseq_logger.setup_logger()
# Module imports
import pyinseq
from pyinseq.utils import read_gene_file, tab_delimited_samples_to_dict
from pyinseq.demultiplex import demultiplex_fastq
from pyinseq.gbk_convert import build_fna_and_table_files
from pyinseq.process_mapping import map_sites, map_genes, build_gene_table
from pyinseq.settings import Settings
from pyinseq.pipeline import pipeline_summarize
from pyinseq.map_reads import summarize_mapping

# Path to environment folder
ENVS = Path(pyinseq.__file__).parent.joinpath("envs")
### Grab from config
command = config['command']
reads = config['input']
samples = config['samples']
experiment = config['experiment']
reference_genome = config['genome']
output_dir = f"results/{experiment}"

report: f"{output_dir}/workflow.rst"

# Set attributes from config
settings = Settings(experiment)
settings.write_trimmed_reads = False if config['notrim'] else True
settings.set_disruption(config['disruption'])
settings.set_gene_parameters(config['min_count'], config['max_ratio'])
samples_dict = tab_delimited_samples_to_dict(samples)

# Setup pyinseq logger and add file handler to snakemake
snake_logger = logger
# Set a NULLHANDLER TO QUIET SNAKEMAKE BUT STILL WRITE TO IO
print(snake_logger.logger.handlers)
# snake_logger.logger.disabled = False
snake_logger.quiet = True
pyinseq_logger.add_logfile_paths(settings)
add_fileHandler(pyinseq_logger.logger, settings.log, formatter=True)
add_fileHandler(logger.logger, settings.log)
# Add snake_io as handler to snakemake logger
add_streamHandler(snake_logger.logger, pyinseq_logger.snake_io)


rule all:
    input:
        f'{output_dir}/summary_gene_table.txt',
        expand(output_dir + "/raw_data/{sample}.fastq", sample=samples_dict.keys()),
        expand(output_dir + "/{sample}_trimmed.fastq", sample=samples_dict.keys()),
        expand(output_dir + "/{sample}_bowtie.txt", sample=samples_dict.keys()),
        expand(output_dir + "/{sample}_sites.txt", sample=samples_dict.keys()),
        expand(output_dir + "/{sample}_genes.txt", sample=samples_dict.keys()),
        f"{output_dir}/samples.txt",


rule genome_prep:
    input:
        genebank=f"{reference_genome}"
    output:
        fna_file="{output_dir}/genome_lookup/genome.fna",
        ftt_file="{output_dir}/genome_lookup/genome.ftt",
    threads: config['threads']
    run:
        # Build needed genome tables
        build_fna_and_table_files(input.genebank, settings)


rule bowtie_index:
    input:
        fna_file=f"{output_dir}/genome_lookup/genome.fna"
    output:
        "{output_dir}/genome_lookup/genome.1.ebwt",
    threads: config['threads']
    conda:
        str(ENVS.joinpath("bowtie.yaml"))
    shell:
        "bowtie-build -q {input.fna_file} {output_dir}/genome_lookup/genome"


rule demultiplex:
    input:
        fastq=f"{reads}"
    output:
        expand(output_dir + "/raw_data/{sample}.fastq", sample=samples_dict.keys()),
        expand(output_dir + "/{sample}_trimmed.fastq", sample=samples_dict.keys())
    threads: 1
    run:
        # Run demultiplex
        demultiplex_fastq(input.fastq, samples_dict, settings)
        pyinseq_logger.summarize_step()


rule bowtie_mapping:
    input:
        output_dir + "/{sample}_trimmed.fastq",
        f"{output_dir}/genome_lookup/genome.1.ebwt"
    output:
        output_dir + "/{sample}_bowtie.txt"
    threads: 2
    conda:
        str(ENVS.joinpath("bowtie.yaml"))
    params:
        genome=f"{output_dir}/genome_lookup/genome",
        log=settings.log,
        summary_log=settings.summary_log
    script:
        f"../../map_reads.py"



rule map_sites:
    input:
        output_dir + "/{sample}_bowtie.txt"
    output:
        output_dir + "/{sample}_sites.txt",
    threads: 1
    run:
        # Map sites
        map_sites(wildcards.sample, settings)
        pyinseq_logger.summarize_step()


rule map_genes:
    input:
        output_dir + "/{sample}_sites.txt",
    output:
        output_dir + "/{sample}_genes.txt"
    threads: 1
    run:
        # Map genes
        map_genes(wildcards.sample, settings)
        pyinseq_logger.summarize_step()


rule build_gene_table:
    input:
        expand(output_dir + "/{sample}_genes.txt", sample=samples_dict.keys()),
    output:
        "{output_dir}/summary_gene_table.txt"
    threads: 1
    run:
        # Get gene_mappings dictionary
        gene_mappings = dict()
        for sample_gene_table in input:
            from pathlib import Path
            sample = Path(sample_gene_table).name.replace('_genes.txt', '')
            gene_mappings[sample] = read_gene_file(sample_gene_table)
        # Map sites
        build_gene_table(settings.organism, samples_dict, gene_mappings, settings.experiment)


rule summarize:
    input:
        output_dir + "/summary_gene_table.txt"
    output:
        "{output_dir}/samples.txt"
    threads: 1
    run:
        pipeline_summarize(samples_dict, settings)
        pyinseq_logger.summarize_step()


